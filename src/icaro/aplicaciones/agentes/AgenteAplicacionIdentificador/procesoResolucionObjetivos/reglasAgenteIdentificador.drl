import icaro.infraestructura.recursosOrganizacion.recursoTrazas.ItfUsoRecursoTrazas;
import icaro.infraestructura.recursosOrganizacion.recursoTrazas.imp.componentes.InfoTraza;
import icaro.infraestructura.entidadesBasicas.procesadorCognitivo.*;
import icaro.infraestructura.patronAgenteCognitivo.procesadorObjetivos.gestorTareas.ItfGestorTareas;
import icaro.infraestructura.entidadesBasicas.PerformativaUsuario;
import icaro.infraestructura.entidadesBasicas.comunicacion.*
import icaro.aplicaciones.agentes.AgenteAplicacionIdentificador.objetivos.*;
import icaro.aplicaciones.informacion.gestionCitas.*;
import icaro.aplicaciones.agentes.AgenteAplicacionIdentificador.tareas.*;
import icaro.aplicaciones.agentes.AgenteAplicacionIdentificador.tools.*;

global ItfGestorTareas gestorTareas;
global ItfUsoRecursoTrazas recursoTrazas;
global String agentId;


rule "Creacion de los objectivos iniciales"
when 
then 
TareaSincrona tarea = gestorTareas.crearTareaSincrona(InicializarInfoWorkMem.class);
    tarea.ejecutar();
recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
end

rule "Saludo Inicial"
when
then
   // recursoTrazas.aceptaNuevaTraza(new InfoTraza(agentId,"Se ejecuta la tarea : SolicitarDatosAcceso",InfoTraza.NivelTraza.debug));
     TareaSincrona tarea = gestorTareas.crearTareaSincrona(SaludoInicial.class);
     tarea.ejecutar(VocabularioGestionCitas.IdentRecursoComunicacionChat);
   // recursoTrazas.aceptaNuevaTraza(new InfoTraza(agentId,"Realizando el objetivo : "+obj.getgoalId()+"  Ejecutando la tarea : "+ tarea.getIdentTarea() ,InfoTraza.NivelTraza.debug));
   // recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
end

rule "Saludo inicial de usuario"
 when
    notif:Notificacion(identInterlc:identNotificador, tipoNotificacion == tipoNotif.saludo)
    not( exists (UsuarioContexto(usuario == identInterlc )))
 then
 	UsuarioContexto cu = new UsuarioContexto();
 	cu.usuario = identInterlc;
 	insert( cu );
 	FocoUsuario fu = new FocoUsuario(identInterlc);
 	Objetivo id = new IdentificarUsuario();
 	//fu.setFoco(id);
 	id.setobjectReferenceId(identInterlc);
 	insert( fu );
 	insert( id );
 //	Objetivo fd = new CrearMemoriaUsuario();
 //	fd.setobjectReferenceId(identInterlc);
 //	insert(fd);
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(identInterlc,conversacion.saludoInicial);
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    retract(notif);
end

rule "Resaludar"
 when
    notif:Notificacion(identInterlc:identNotificador, tipoNotificacion == tipoNotif.saludo)
   	cu:UsuarioContexto(usuario == identInterlc)
 then
  	 TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
     tarea.ejecutar(identInterlc,conversacion.resaludo1);
     recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
     cu.actividad();
     retract(notif);
end


rule "Regla de solicitar nombre"
 when
 	
 	fc:FocoUsuario(fcuser:usuario , foco == null)
 	obj:IdentificarUsuario(state == Objetivo.PENDING ,objectReferenceId == fcuser )
 	not( exists (ObtenerNombreUsuario(objectReferenceId == fcuser)))
 then
  	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
  	Objetivo no = new ObtenerNombreUsuario();
  	no.setobjectReferenceId(fcuser);
  	fc.setFoco(no);
  	
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(fcuser,conversacion.SolicitarNombre2);
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	update(fc);
   	insert(no);
end

rule "Regla de obtencion de nombre"
 when
	obj:ObtenerNombreUsuario(user:objectReferenceId, state == Objetivo.PENDING)
 	fc:FocoUsuario(usuario == user, foco == obj )
    notif:Notificacion(identNotificador == user, tipoNotificacion == tipoNotif.nombre, msgg:mensajeNotificacion )
    cu:UsuarioContexto(usuario == user)
 then
 	obj.setSolved();
 	cu.setNombre(msgg);
 	cu.actividad();
 	fc.setFoco(null);
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(SolicitarNombre.class);
    tarea.ejecutar(user,conversacion.obtencionNombre1,msgg);
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	update(obj);
   	update(fc);
   	retract(notif);
end


rule "Regla de solicitud de obtener DNI"
 when
 	fc:FocoUsuario(fcuser:usuario , foco == null)
 	obj:IdentificarUsuario(state == Objetivo.PENDING ,objectReferenceId == fcuser )
 	not( exists (ObtenerDNIUsuario(objectReferenceId == fcuser)))
 	cu:UsuarioContexto(usuario == fcuser)
 then

  	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
  	Objetivo ob = new ObtenerDNIUsuario();	
	ob.setobjectReferenceId(fcuser);
	insert(ob);
	fc.setFoco(ob);
  	TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
  	if(cu.getNombre() == null){
  	   tarea.ejecutar(fcuser, "Para acelerar un poco las cosas necesito que de digas tu DNI");	
  	}else{
  	   tarea.ejecutar(fcuser, cu.getNombre()+ ", " + conversacion.SolicitarDNI1);
  	}
  
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   update(obj);
   update(fc);
end

rule "Regla de obtencion de dni"
 when
 	obj:ObtenerDNIUsuario(user:objectReferenceId, state == Objetivo.PENDING)
 	fc:FocoUsuario(usuario == user, foco == obj )
    notif:Notificacion(identNotificador == user, tipoNotificacion == tipoNotif.dni, msgg:mensajeNotificacion )
    cu:UsuarioContexto(usuario == user)
 then
 	obj.setSolved();
 	cu.setDNI(msgg);
 	cu.actividad();
 	fc.setFoco(null);
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(user,conversacion.obtencionDNI1);
   	recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   	
   	update(obj);
   	update(fc);
   	retract(notif);
end


rule "Finalizar la identificacion"
 when
 	ObtenerDNIUsuario(user:objectReferenceId, state == Objetivo.SOLVED);
 	ObtenerNombreUsuario(objectReferenceId == user, state == Objetivo.SOLVED)
 	obj:IdentificarUsuario(state == Objetivo.PENDING ,objectReferenceId == user )
 then
 	obj.setSolved();
 	update(obj);
end


rule "Focalizacion de distribucion de mensajes"
 when
    obj:IdentificarUsuario(state == Objetivo.SOLVED ,user:objectReferenceId)
    cu:UsuarioContexto(usuario == user)
    fc:FocoUsuario(usuario == user)
 then
	obj.setSolving();
	Objetivo dm = new DistribuirMensajes();
	dm.setobjectReferenceId(user);
	fc.setFoco(dm);

  TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
     tarea.ejecutar(user, cu.getNombre()+ ", " + conversacion.distribucion1);
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   update(obj);
   	insert(dm);
	update(fc);
end

rule "Distribucion de mensajes paciente"
 when
    notif:Notificacion(identInterlc:identNotificador, tipoNotificacion != tipoNotif.saludo && tipoNotificacion != tipoNotif.despedida)
    obj:DistribuirMensajes(state == Objetivo.SOLVING, objectReferenceId == identInterlc)
    fc:FocoUsuario(usuario == identInterlc, foco == obj)
    cu:UsuarioContexto(usuario == identInterlc)
 then
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
	cu.actividad();
  Tarea tarea = gestorTareas.crearTarea(Distribuir.class);
     tarea.ejecutar(identInterlc, notif);
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   retract(notif);
end

rule "Detectar Inactividad"
	when 
	 cu:UsuarioContexto(user:usuario, inactividad(1) );
	 not( exists Inactividad(user == objectReferenceId ))
	 then
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
	Objetivo obb = new Inactividad();
	obb.setobjectReferenceId(user);
	insert( obb );
	cu.actividad();
  TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
     tarea.ejecutar(cu.getUsuario(), cu.getNombre() +", "+ conversacion.inactividad1);
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
end

rule "Detectar Inactividad prolongada"
	when 
 	 cu:UsuarioContexto(user:usuario, inactividad(2) );
	 Inactividad(user == objectReferenceId, state == Objetivo.PENDING );
	 
	 then
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
	//m.eliminarUsuario(usuario);
  TareaSincrona tarea2 = gestorTareas.crearTareaSincrona(EliminarSessionUsuario.class);
     tarea2.ejecutar(cu.usuario);
  TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
     tarea.ejecutar(user,  conversacion.despedida1);
   
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   retract( cu );
   //update(m);
end

rule "Despedirse"
	when 
	 notif:Notificacion(identInterlc:identNotificador, tipoNotificacion == tipoNotif.despedida)
 	 cu:UsuarioContexto(usuario == identInterlc );
	 then
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTARA LA REGLA: " + drools.getRule().getName());
	//m.eliminarUsuario(usuario);
   TareaSincrona tarea = gestorTareas.crearTareaSincrona(EliminarSessionUsuario.class);
     tarea.ejecutar(cu.usuario);
   TareaSincrona tarea2 = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
     tarea2.ejecutar(cu.usuario,  conversacion.despedida1);
   recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName());
   retract( cu );
   retract( notif );
   //update(m);
end




	
	
rule "Timer Inactividad"
	timer ( int: 1m )
	when 
	 m:UsuarioContexto()
	 then
   update(m);
end
	

