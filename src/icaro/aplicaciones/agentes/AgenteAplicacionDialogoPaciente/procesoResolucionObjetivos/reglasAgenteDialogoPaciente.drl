import icaro.infraestructura.recursosOrganizacion.recursoTrazas.ItfUsoRecursoTrazas;
import icaro.infraestructura.recursosOrganizacion.recursoTrazas.imp.componentes.InfoTraza;
import icaro.infraestructura.entidadesBasicas.procesadorCognitivo.*;
import icaro.infraestructura.patronAgenteCognitivo.procesadorObjetivos.gestorTareas.ItfGestorTareas;
import icaro.infraestructura.entidadesBasicas.PerformativaUsuario;
import icaro.infraestructura.entidadesBasicas.comunicacion.*
import icaro.aplicaciones.agentes.AgenteAplicacionDialogoPaciente.objetivos.*;
import icaro.aplicaciones.informacion.gestionCitas.*;
import icaro.aplicaciones.agentes.AgenteAplicacionDialogoPaciente.tareas.*;
import icaro.aplicaciones.agentes.AgenteAplicacionDialogoPaciente.tools.*;

global ItfGestorTareas gestorTareas;
global ItfUsoRecursoTrazas recursoTrazas;
global String agentId;

rule "Inicio obtención datos cita"
 when
    notif:NotificacionPaciente(identInterlc:identNotificador, tipoNotificacion == tipoNotifPaciente.inicioPeticion);
 then
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    // la funcion ejecutar recibe dos strings, el identificador de usuario(dni) y el mensaje que quieras enviarle.
    tarea.ejecutar(identInterlc,conversacionPaciente.obtenerInfoCita1dameDatos);
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    retract(notif);
 	Objetivo id = new IdentificarMedico();
 	// Objetivo id2 = new ObtenerFechaCita();
 	id.setSolving();
 	id.setobjectReferenceId(identInterlc);
 	insert(id);
end

rule "Obtención datos médico cita, antes que la fecha"
 when
    notif:NotificacionPaciente(identInterlc:identNotificador, tipoNotificacion == tipoNotifPaciente.nombre, msgg:mensajeNotificacion )
    not( exists ObtenerFechaCita(state == Objetivo.SOLVED, objectReferenceId == identInterlc)) 
    obj:IdentificarMedico(state == Objetivo.SOLVING, objectReferenceId == identInterlc)
    // cu:UsuarioContexto(usuario == identInterlc)
 then
 	obj.setSolved();
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(identInterlc,conversacionPaciente.obtenerInfoCita3medicoRegistrado);
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    Objetivo nobj = new ObtenerFechaCita();
 	nobj.setobjectReferenceId(identInterlc);
 	nobj.setSolving();
 	insert(nobj);
 	update(obj);
    retract(notif);
end

rule "Obtención datos médico cita, teniendo la fecha"
 when
    notif:NotificacionPaciente(identInterlc:identNotificador, tipoNotificacion == tipoNotifPaciente.nombre, msgg:mensajeNotificacion )
    //notif:NotificacionPaciente(identInterlc:identNotificador, tipoNotificacion == tipoNotifPaciente.nombre, msgg:mensajeNotificacion )
    ObtenerFechaCita(state == Objetivo.SOLVED, objectReferenceId == identInterlc) 
    obj:IdentificarMedico(state == Objetivo.SOLVING, objectReferenceId == identInterlc)
    // cu:UsuarioContexto(usuario == identInterlc)
 then
 	obj.setSolved();
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(identInterlc,conversacionPaciente.obtenerInfoCita4todoCompletado);
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    retract(notif);
end

rule "Obtención datos fecha cita, antes que el médico"
 when
    notif:NotificacionPaciente(identInterlc:identNotificador, tipoNotificacion == tipoNotifPaciente.fecha, msgg:mensajeNotificacion )
    not( exists IdentificarMedico(state == Objetivo.SOLVED, objectReferenceId == identInterlc))
    obj:ObtenerFechaCita(state == Objetivo.SOLVING, objectReferenceId == identInterlc)
    // cu:UsuarioContexto(usuario == identInterlc)
 then
 	obj.setSolved();
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(identInterlc,conversacionPaciente.obtenerInfoCita2fechaRegistrada);
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    Objetivo nobj = new IdentificarMedico();
 	nobj.setobjectReferenceId(identInterlc);
 	nobj.setSolving();
 	insert(nobj);
 	update(obj);
    retract(notif);
end

rule "Obtención datos fecha cita, teniendo el médico"
 when
    notif:NotificacionPaciente(identInterlc:identNotificador, tipoNotificacion == tipoNotifPaciente.fecha, msgg:mensajeNotificacion )
    IdentificarMedico(state == Objetivo.SOLVED, objectReferenceId == identInterlc)
    obj:ObtenerFechaCita(state == Objetivo.SOLVING, objectReferenceId == identInterlc)
    // cu:UsuarioContexto(usuario == identInterlc)
 then
 	obj.setSolved();
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(identInterlc,conversacionPaciente.obtenerInfoCita4todoCompletado);
    
    TareaSincrona tarea2 = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    // TODO - Coger el nombre del medico y la fecha del recurso cita
    tarea2.ejecutar("medico","El paciente " + identInterlc + " ha pedido una cita para la fecha fecha.");
    
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    retract(notif);
end

// Regla para consultar las citas por parte del paciente
rule "Consultar citas, teniendo el paciente"
 when
    notif:NotificacionPaciente(identInterlc:identNotificador, tipoNotificacion == tipoNotifPaciente.consulta, msgg:mensajeNotificacion )
 then
    TareaSincrona tarea = gestorTareas.crearTareaSincrona(MensajeGenerico.class);
    tarea.ejecutar(identInterlc,conversacionPaciente.mostrarCitasPaciente);
    // aqui habria que ir al recurso calendario para obtener las citas una a una
    recursoTrazas.aceptaNuevaTrazaEjecReglas(agentId," EJECUTO LA REGLA: " + drools.getRule().getName()); 
    retract(notif);
end
